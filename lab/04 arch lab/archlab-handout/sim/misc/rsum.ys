# Execution starts at address 0
    .pos 0
    irmovq stack, %rsp
    call main
    halt

# Sample linked list
.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

main:
    irmovq ele1, %rdi
    call rsum_list
    ret

# long rsum_list(list_ptr ls)
# ls in %rdi
rsum_list:
    xorq %rax, %rax         # Intialize val to 0
    andq %rdi, %rdi         # ls ?= 0
    je return
    pushq %rbx              # Push callee-saved reg
    mrmovq (%rdi), %rbx     # Save current node value
    mrmovq 8(%rdi), %rdi    # Move the node to next
    call rsum_list          # Recursively call
    addq %rbx, %rax         # Add return value and current node value
    popq %rbx               # Pop callee-saved reg
return:
    ret

# stack here
    .pos 0x200
stack:
